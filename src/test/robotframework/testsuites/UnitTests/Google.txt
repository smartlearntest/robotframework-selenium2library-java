*** Settings ***
Suite Setup       Google.Site.Open
Suite Teardown    Google.Site.Close
Test Setup        Google.Site.Init
Resource          ../../settings/Settings.txt
Resource          ../../imports/Google.txt

*** Testcases ***
Open And Close Google Site
    Capture Page Screenshot
    
Store Web Element In JavaScript
    Execute Javascript    window.document.my_element = window.document.getElementsByClassName('gbqfba')[0];
    ${className}=    Execute Javascript    return window.document.my_element.className;
    Should Be Equal    ${className}    gbqfba

Search Robotframework Selenium2Library
    Google.Search.Search String    Robotframework Selenium2Library
    
Search With JavaScript Locator
    Selenium2Library.Add Location Strategy    elementById    return window.document.getElementById(arguments[0]);
    Input Text    elementById=gbqfq    Robotframework Selenium2Library Java
    Press Key    elementById=gbqfq    \\13
    Wait Until Element Is Visible    xpath=//a[contains(.,'markusbernhardt')]
    Capture Page Screenshot
    
Search With JavaScript Locator And Delimiter
    Selenium2Library.Add Location Strategy    elementByClassname    return window.document.getElementsByClassName(arguments[0])[arguments[1]];    ,
    Element Text Should Be    elementByClassname=gbqfba,0    Google-Suche
    Element Text Should Be    elementByClassname=gbqfba,1    Auf gut Gl√ºck!

Search With JavaScript Locator Text
    Selenium2Library.Add Location Strategy    text    return window.document.evaluate("//*[contains(text(),'" + arguments[0] + "')]", window.document, null, XPathResult.FIRST_ORDERED_NODE_TYPE, null).singleNodeValue;
    Wait Until Element Is Visible    text=Google-Suche

Search Without Locator Type
    Input Text    gbqfq    Robotframework Selenium2Library Java
    Press Key    gbqfq    \\13
    Wait Until Element Is Visible    //a[contains(.,'markusbernhardt')]
    
Get Id Of Active Element With JavaScript
    Input Text    gbqfq    Robotframework Selenium2Library
    ${activeElementId}=    Execute JavaScript    return window.document.activeElement.id;
